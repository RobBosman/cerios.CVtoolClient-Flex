<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:s="library://ns.adobe.com/flex/spark"
				xmlns:gui="nl.bransom.gui.*"
				xmlns:cvtool="nl.cerios.cvtool.*"
				xmlns:cvadmin="nl.cerios.cvtool.cvadmin.*"
				xmlns:cveditor="nl.cerios.cvtool.cveditor.*"
				creationComplete="init()">
	
	<fx:Style source="CVtool.css"/>
	
	<fx:Script>
		<![CDATA[
			import mx.binding.utils.BindingUtils;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			
			import nl.bransom.ChainableTask;
			import nl.bransom.MD5;
			import nl.bransom.PersistencyStatus;
			import nl.bransom.XmlUtils;
			import nl.bransom.gui.Utils;
			import nl.bransom.rest.RestClient;
			
			[Embed(source="/assets/Valori_name.png")] public static const VALORI_NAME:Class;
			[Embed(source="/assets/CVtool.png")] public static const CVTOOL_IMG:Class;
			[Embed(source="/assets/icon_create.png")] public static const ICON_CREATE:Class;
			[Embed(source="/assets/icon_change.png")] public static const ICON_CHANGE:Class;
			[Embed(source="/assets/icon_delete.png")] public static const ICON_DELETE:Class;
			[Embed(source="/assets/icon_flag_NL.png")] public static const ICON_FLAG_NL:Class;
			[Embed(source="/assets/icon_flag_UK.png")] public static const ICON_FLAG_UK:Class;
			
			public static const APP_TITLE:String = "Valori CVtool";
			public static const APP_VERSION:String = "v20200124";
			public static const ROL_EDIT_EIGEN_CV:String = "edit eigen CV";
			public static const ROL_VIEW_ALLE_CVS:String = "view alle CVs";
			public static const REST_APP:String = "cv";
			private static const AUTH_URL:String = "/cvtool/cvtool.php";
			private static const REST_BASE_URL:String = "/bransom/REST/";
			
			public static const LOCALE_INFO:Array = [
				{label:'Nederlands', locale:'nl_NL', icon:ICON_FLAG_NL},
				{label:'Engels', locale:'uk_UK', icon:ICON_FLAG_UK}
			];
			
			[Bindable] public static var selectedLocaleInfo:Object;
			[Bindable] private var restClient:RestClient;
			[Bindable] private var signedInAccountXml:XML;
			
			private function init():void {
				selectedLocaleInfo = LOCALE_INFO[0];
				restClient = new RestClient(REST_BASE_URL, REST_APP, AUTH_URL);
				BindingUtils.bindSetter(setSignedInAccountId, restClient, "signedInAccountId");
				
				currentState = "INIT";
				
				var splashScreenDelayTimer:Timer = new Timer(1000);
				splashScreenDelayTimer.repeatCount = 1;
				splashScreenDelayTimer.start();
				splashScreenDelayTimer.addEventListener(TimerEvent.TIMER, splashScreenDelayExpired);
				
				restClient.getAuthorization();
			}
			private function splashScreenDelayExpired(event:Event = null):void {
				if (signedInAccountXml == null) {
					currentState = "SIGNED_OUT";
				}
			}
			private function setSignedInAccountId(signedInAccountId:String):void {
				if (signedInAccountId != null) {
					// Fetch the credentials.
					var restUrlParams:Object = {
						"$scope":"Pa",
						"rol/$scope":"P"
					};
					restClient.read("CREDENTIALS", "_account/" + signedInAccountId, restUrlParams, setSignedInAccountXml);
				} else {
					// Switch to the signed-out state.
					currentState = "SIGNED_OUT";
				}
			}
			private function setSignedInAccountXml(signedInAccountXml:XML):void {
				this.signedInAccountXml = signedInAccountXml;
				if (signedInAccountXml == null) {
					// Switch to the signed-out state.
					currentState = "SIGNED_OUT";
				} else {
					// Check if a 'manager' signed in.
					if (hasRol(signedInAccountXml, ROL_VIEW_ALLE_CVS)) {
						currentState = "SIGNED_IN_ADMIN";
					} else {
						currentState = "SIGNED_IN_STAFF";
					}
				}
			}
			
			private function hasRol(accountXml:XML, rolNaam:String):Boolean {
				var rolList:XMLList = XmlUtils.getElements(accountXml, "rol")
				for (var i:uint = 0; i < rolList.length(); i++) {
					if (XmlUtils.getTextContent(rolList[i], "naam") == rolNaam) {
						return true;
					}
				}
				return false;
			}
			
			private function getSignInText():String {
				return "Om de CVtool te gebruiken moet je je aanmelden met je <b>Valori</b> account."
					+ "\nTijdens het inlogproces controleert de CVtool je account, je moet daar eenmalig toestemming voor geven."
					+ "\n\n<a target='blank' href='https://account.activedirectory.windowsazure.com/r/#/applications'><u><b>Hier</b></u></a>"
					+ " kun je die toestemming bekijken en eventueel weer intrekken."
					+ "\nAls je de <i>Valori CVtool</i> app verwijdert blijven je cv-gegevens bewaard."
					+ "\nNa opnieuw inloggen (en toestemming verlenen) kun je daar weer bij."
					+ "\n<i>Problemen? <a target='blank' href='mailto:RobBosman@valori.nl?subject=CVtool'><u>Mail</u></a> even!</i>";
			}

			private function signOut():void {
				if (restClient.status != PersistencyStatus.CHANGED) {
					// No changes to save, just sign-out.
					signOutChainableMethod();
				} else if (restClient.useAutoSave) {
					// First save any changes and then sign-out.
					restClient.save(null, new ChainableTask(signOutChainableMethod));
				} else {
					var message:String = "Wijzigingen opslaan?";
					Utils.alert(message, "Bevestigen", Alert.YES | Alert.NO | Alert.CANCEL, this, confirmSignOutHandler,
						null, Alert.YES);
				}
			}
			private function confirmSignOutHandler(closeEvent:CloseEvent):void {
				if (closeEvent.detail == Alert.YES) {
					// First save any changes and then sign-out.
					restClient.save(null, new ChainableTask(signOutChainableMethod));
				} else if (closeEvent.detail == Alert.NO) {
					signOutChainableMethod();
				}
			}
			private function signOutChainableMethod(dummy:* = null, nextTask:ChainableTask = null):void {
				restClient.signOut();
			}
		]]>
	</fx:Script>
	
	<mx:states>
		<mx:State name="INIT"/>
		<mx:State name="SIGNED_OUT"/>
		<mx:State name="SIGNED_IN_ADMIN"/>
		<mx:State name="SIGNED_IN_STAFF"/>
	</mx:states>
	
	<mx:transitions>
		<mx:Transition fromState="INIT,SIGNED_OUT" toState="*">
			<mx:Parallel>
				<mx:Fade targets="{[signInTab, cvAdmin, cvEditor, signedInStuff, statusControlBar]}"/>
				<mx:Move targets="{[logo, logoBar]}"/>
			</mx:Parallel>
		</mx:Transition>
		<mx:Transition fromState="*" toState="SIGNED_OUT">
			<mx:Parallel>
				<mx:Fade targets="{[signInTab, cvAdmin, cvEditor, signedInStuff, statusControlBar]}"/>
				<mx:Move targets="{[logo, logoBar]}"/>
			</mx:Parallel>
		</mx:Transition>
	</mx:transitions>

	<gui:DebugControlBar id="debugControlBar" width="100%" appTitle="{APP_TITLE}"
						 restClient="{restClient}" styleName="debug-control-bar"/>

	<mx:Canvas width="100%" height="100%">
		<mx:Image right="20" bottom="0" width="100%" height="70%" alpha="0.1" horizontalAlign="right"
				  source="{CVTOOL_IMG}"/>
		<mx:VBox width="100%" height="100%">
			<mx:HBox id="logoBar" width="100%" horizontalAlign="center" verticalAlign="top"
					 height.INIT="40%" verticalAlign.INIT="bottom"
					 height.SIGNED_OUT="40%" verticalAlign.SIGNED_OUT="bottom">
				<mx:HBox id="logo" styleName="title" verticalAlign="middle"
						 x.SIGNED_IN_ADMIN="0" y.SIGNED_IN_ADMIN="0"
						 x.SIGNED_IN_STAFF="0" y.SIGNED_IN_STAFF="0">
					<mx:Image scaleX="0.32" scaleY="0.32" source="{VALORI_NAME}"/>
					<mx:Label text="CVtool"/>
				</mx:HBox>
				<mx:VBox includeIn="SIGNED_IN_ADMIN,SIGNED_IN_STAFF" width="100%" height="100%">
					<mx:HBox id="signedInStuff" width="100%" verticalAlign="middle">
						<mx:Label top="0" styleName="version" text="{APP_VERSION}"/>
						<mx:Spacer width="100%"/>
						<mx:Label htmlText=""/><!-- dummy label to prevent the next label from mysteriously disappearing... -->
						<mx:Label htmlText="aangemeld als &lt;b&gt;{           XmlUtils.getTextContent(signedInAccountXml, 'name').replace(/\./g, ' ')           }&lt;/b&gt;"
								  verticalCenter="0"/>
						<mx:Button label="Afmelden" click="signOut()" verticalCenter="0"/>
					</mx:HBox>
					<mx:HBox id="language" width="100%" horizontalAlign="right"
							 verticalAlign="middle">
						<mx:Label text="taalkeuze"/>
						<mx:ComboBox id="localeCombo"
									 change="CVtool.selectedLocaleInfo = localeCombo.selectedItem"
									 dataProvider="{CVtool.LOCALE_INFO}"
									 selectedItem="{CVtool.selectedLocaleInfo}"/>
					</mx:HBox>
				</mx:VBox>
			</mx:HBox>
			
			<mx:VBox id="signInTab" includeIn="SIGNED_OUT" width="100%" horizontalAlign="center"
					 horizontalCenter="0">
				<mx:TextArea width="550" height="150" editable="false" htmlText="{getSignInText()}"
							 styleName="info" textAlign="center"/>
				<mx:Button id="signInButton" label="Aanmelden" click="restClient.signIn()"/>
			</mx:VBox>

			<mx:ApplicationControlBar id="cvAdmin" includeIn="SIGNED_IN_ADMIN" width="100%"
									  height="100%" styleName="main-container">
				<cvtool:CvAdminTabs width="100%" height="100%" restClient="{restClient}"
									signedInAccountXml="{signedInAccountXml}"
									styleName="main-container"/>
			</mx:ApplicationControlBar>
			<mx:ApplicationControlBar id="cvEditor" includeIn="SIGNED_IN_STAFF" width="100%"
									  height="100%">
				<cvtool:CvEditorTabs width="100%" height="100%" restClient="{restClient}"
									 signedInAccountXml="{signedInAccountXml}"
									 styleName="main-container"/>
			</mx:ApplicationControlBar>
		</mx:VBox>
	</mx:Canvas>
	
	<gui:StatusControlBar id="statusControlBar" excludeFrom="INIT,SIGNED_OUT" width="100%"
						  restClient="{restClient}"/>
</mx:Application>

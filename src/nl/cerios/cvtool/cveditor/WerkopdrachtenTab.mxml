<?xml version="1.0" encoding="utf-8"?>
<mx:HDividedBox xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:gui="nl.bransom.gui.*"
				xmlns:cveditor="nl.cerios.cvtool.cveditor.*"
				enabled="{parentXml != null}"
				creationComplete="init()">
	
	<fx:Script>
		<![CDATA[
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			
			import nl.bransom.XmlUtils;
			import nl.bransom.gui.Utils;
			import nl.bransom.rest.RestClient;
			
			public static const DATAGRID_FIELD_NAMES:ArrayCollection = new ArrayCollection(["",
				"werkgever", "opdrachtgever", "titel", "periode_begin", "periode_eind", "opnemen_in_cv"]);
			
			[Bindable] public var restClient:RestClient;
			[Bindable] public var parentXml:XML;
			[Bindable] public var objectName:String;
			
			[Bindable] public static var _restClient:RestClient;
			private static var isWarned:Boolean = false;

			private function init():void {
				BindingUtils.bindSetter(setRestClient, this, "restClient");
				BindingUtils.bindSetter(refreshXmlList, this, "parentXml");
				BindingUtils.bindSetter(refreshXmlList, this, "objectName");
			}
			private function setRestClient(restClient:RestClient):void {
				_restClient = restClient;
			}
			
			private function updateItemSortIndices(event:Event = null):void {
				var xmlList:XMLList = dataGrid.getXmlData();
				if ((xmlList != null) && (xmlList.length() > 0)) {
					if ((!isWarned) && (!hasSortIndices(xmlList))) {
						Utils.alert("Sorteren van werkervaringen gaat nu NIET meer automatisch.\n"
							+ "Je kunt (moet!) de volgorde met drag&drop zelf bepalen.", "LET OP");
						isWarned = true;
					}
					// Give all <werkopdracht> items their new sort_index, based on the XMLList of the dataGrid.
					var index:int = 1;
					for each (var xmlItem:XML in xmlList) {
						restClient.setTextContent(xmlItem, "sort_index", String(index));
						index++;
					}
				}
			}
			
			private function createItem(event:Event = null):void {
				var createdXml:XML = restClient.createEntityNode(parentXml, objectName, 0);
				restClient.setTextContent(createdXml, "sort_index", "-1");
				restClient.setTextContent(createdXml, "opnemen_in_cv", "1");
				// Add a <werkervaring> for nl_NL and uk_UK.
				var werkervaringXmlNL:XML = restClient.createEntityNode(createdXml, "werkervaring");
				restClient.setTextContent(werkervaringXmlNL, "locale", "nl_NL");
				var werkervaringXmlUK:XML = restClient.createEntityNode(createdXml, "werkervaring");
				restClient.setTextContent(werkervaringXmlUK, "locale", "uk_UK");

				// For some reason it is not possible to prepend the new node to the dataGrid with prependChild().
				// So first explicitly (re)set all existing sort_indices, ...
				updateItemSortIndices();
				// ...then update the GUI...
				refreshXmlList();
				dataGrid.selectedItem = createdXml;
				dataGrid.scrollToIndex(dataGrid.selectedIndex);
				// ..and finally set the correct sort_indices.
				updateItemSortIndices();
			}
			
			private function deleteItem(xmlToBeDeleted:XML):void {
				var message:String = "Moet de geselecteerde werkervaring";
				var titel:String = XmlUtils.getTextContent(xmlToBeDeleted, "titel");
				if (titel.length > 0) {
					message += " '" + titel + "'";
				}
				message += " bij '" + XmlUtils.getTextContent(xmlToBeDeleted, "opdrachtgever")
					+ "' definitief worden verwijderd?";
				Utils.alert(message, "Bevestigen", Alert.YES | Alert.NO, this, confirmDeleteHandler, null, Alert.NO);
			}
			private function confirmDeleteHandler(closeEvent:CloseEvent):void {
				if (closeEvent.detail != Alert.YES) {
					return;
				}
				var xmlToBeDeleted:XML = dataGrid.selectedItem as XML;
				restClient.deleteEntityNode(xmlToBeDeleted);
				// Refresh the GUI.
				refreshXmlList();
			}
			
			private function refreshXmlList(dummy:Object = null):void {
				if ((parentXml != null ) && (objectName != null)) {
					dataGrid.setXmlData(sortXmlList(XmlUtils.getElements(parentXml, objectName)));
				} else {
					dataGrid.setXmlData(null);
				}
			}
			
			private function sortXmlList(xmlList:XMLList):XMLList {
				if (xmlList.length() > 0) {
					// If sort_indices have been set....
					if (hasSortIndices(xmlList)) {
						// ...then sort by "sort_index".
						xmlList = XmlUtils.sortXmlList(xmlList, "sort_index", Array.NUMERIC);
					} else {
						// ...else sort by "periode_eind".
						xmlList = XmlUtils.sortXmlList(xmlList, "periode_eind", Array.DESCENDING, true);
					}
				}
				return xmlList;
			}
			
			private function hasSortIndices(xmlList:XMLList):Boolean {
				// Check if sort_indices have been set to all items.
				for each (var xmlItem:XML in xmlList) {
					var sortIndex:String = XmlUtils.getTextContent(xmlItem, "sort_index");
					if (sortIndex == "") {
						return false;
					}
				}
				return true;
			}
			
			private function getWerkervaringXml(item:Object, selectLocale:String):XML {
				var werkervaringXmlList:XMLList = XmlUtils.getElements(item as XML, "werkervaring");
				for each (var werkervaringXml:XML in werkervaringXmlList) {
					var locale:String = XmlUtils.getTextContent(werkervaringXml, "locale");
					if (locale == selectLocale) {
						return werkervaringXml;
					}
				}
				return null;
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<fx:Component id="cmpWerkgever">
			<gui:XmlTextInput restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
							  propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(1)}" />
		</fx:Component>
		<fx:Component id="cmpOpdrachtgever">
			<gui:XmlTextInput restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
							  propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(2)}" />
		</fx:Component>
		<fx:Component id="cmpTitel">
			<gui:XmlTextInput restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
							  propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(3)}" />
		</fx:Component>
		<fx:Component id="cmpPeriodeBegin">
			<cveditor:XmlPeriodField restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
									 propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(4)}"
									 toolTip="vul het begin van de periode in (MM-JJJJ)"
									 emptyText="MM-JJJJ" emptyStyle="error-text" errorStyle="error-text"
									 errorString="{text.match(/[0-9]{2}\-[0-9]{4}/) ? null : 'datumformaat: MM-JJJJ'}" />
		</fx:Component>
		<fx:Component id="cmpPeriodeEind">
			<cveditor:XmlPeriodField restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
									 propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(5)}"
									 toolTip="vul het einde van de periode in (MM-JJJJ) of laat leeg voor 'heden'"
									 emptyText="heden" emptyStyle="default-text" errorStyle="error-text"
									 errorString="{((text == '') || (text == emptyText) || (text.match(/[0-9]{2}\-[0-9]{4}/))) ? null : 'datumformaat: MM-JJJJ of leeg (= \'heden\')'}" />
		</fx:Component>
		<fx:Component id="cmpOpnemenInCv">
			<gui:XmlCheckBox restClient="{WerkopdrachtenTab._restClient}" objectXml="{data as XML}"
							 propertyName="{WerkopdrachtenTab.DATAGRID_FIELD_NAMES.getItemAt(6)}" paddingLeft="10"
							 toolTip="geef hier aan of deze werkervaring in het cv moet worden opgenomen" />
		</fx:Component>
	</fx:Declarations>
	
	<mx:VBox width="50%" height="100%">
		<mx:HBox>
			<mx:Button label="Toevoegen" icon="{CVtool.ICON_CREATE}" enabled="{parentXml != null}"
					   click="createItem()" />
			<mx:Button label="Verwijderen" icon="{CVtool.ICON_DELETE}" enabled="{dataGrid.selectedItem != null}"
					   click="deleteItem(dataGrid.selectedItem as XML)" />
		</mx:HBox>
		<gui:XmlDataGrid id="dataGrid" fieldNames="{DATAGRID_FIELD_NAMES}" width="100%" height="100%"
						 sortableColumns="false" orderChangedFunction="updateItemSortIndices">
			<gui:columns>
				<mx:DataGridColumn width="20" resizable="false" sortable="false" />
				<mx:DataGridColumn headerText="Werkgever" width="200"
								   itemRenderer="{cmpWerkgever}" rendererIsEditor="true" />
				<mx:DataGridColumn headerText="Opdrachtgever"
								   itemRenderer="{cmpOpdrachtgever}" rendererIsEditor="true" />
				<mx:DataGridColumn headerText="Titel" width="200"
								   itemRenderer="{cmpTitel}" rendererIsEditor="true" />
				<mx:DataGridColumn headerText="van" width="60" resizable="false"
								   itemRenderer="{cmpPeriodeBegin}" rendererIsEditor="true" />
				<mx:DataGridColumn headerText="tot" width="60" resizable="false"
								   itemRenderer="{cmpPeriodeEind}" rendererIsEditor="true" />
				<mx:DataGridColumn headerText="in cv" width="39" resizable="false"
								   itemRenderer="{cmpOpnemenInCv}" rendererIsEditor="true" />
			</gui:columns>
		</gui:XmlDataGrid>
	</mx:VBox>
	
	<mx:HBox width="35%" height="100%">
		<mx:VBox horizontalAlign="right" verticalAlign="top" height="100%">
			<mx:Label text="Rol" height="{rolGUI.height}" />
			<mx:Text text="Opdracht- formulering" height="{opdrachtformuleringGUI.height}"
					 textAlign="right" width="84" fontWeight="bold" />
			<mx:Text text="Werk- zaamheden" height="{werkzaamhedenGUI.height}"
					 textAlign="right" width="84" />
			<mx:Text text="Resultaat" height="{resultaatGUI.height}"
					 textAlign="right" width="84" />
			<mx:Text text="Steek- woorden" height="{steekwoordenGUI.height}"
					 textAlign="right" width="84" />
		</mx:VBox>
		<mx:VBox width="100%" height="100%">
			<gui:XmlTextInput restClient="{restClient}" objectXml="{getWerkervaringXml(dataGrid.selectedItem, CVtool.selectedLocaleInfo.locale)}"
							  propertyName="rol" width="100%"
							  backgroundAlpha="{dataGrid.selectedIndex == -1 ? 0.0 : 1.0}" id="rolGUI" />
			<gui:XmlTextArea restClient="{restClient}" objectXml="{getWerkervaringXml(dataGrid.selectedItem, CVtool.selectedLocaleInfo.locale)}"
							 propertyName="opdrachtformulering" width="100%" height="40%"
							 backgroundAlpha="{dataGrid.selectedIndex == -1 ? 0.0 : 1.0}" id="opdrachtformuleringGUI" />
			<gui:XmlTextArea restClient="{restClient}" objectXml="{getWerkervaringXml(dataGrid.selectedItem, CVtool.selectedLocaleInfo.locale)}"
							 propertyName="werkzaamheden" width="100%" height="30%"
							 backgroundAlpha="{dataGrid.selectedIndex == -1 ? 0.0 : 1.0}" id="werkzaamhedenGUI" />
			<gui:XmlTextArea restClient="{restClient}" objectXml="{getWerkervaringXml(dataGrid.selectedItem, CVtool.selectedLocaleInfo.locale)}"
							 propertyName="resultaat" width="100%" height="20%"
							 backgroundAlpha="{dataGrid.selectedIndex == -1 ? 0.0 : 1.0}" id="resultaatGUI" />
			<gui:XmlTextArea restClient="{restClient}" objectXml="{getWerkervaringXml(dataGrid.selectedItem, CVtool.selectedLocaleInfo.locale)}"
							 propertyName="steekwoorden" width="100%" height="10%"
							 backgroundAlpha="{dataGrid.selectedIndex == -1 ? 0.0 : 1.0}" id="steekwoordenGUI" />
		</mx:VBox>
	</mx:HBox>
</mx:HDividedBox>
